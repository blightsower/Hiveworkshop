module Transformer
            readonly real x1
            readonly real y1
            readonly real z1
            readonly real x2
            readonly real y2
            readonly real z2

            real t
       
            method setEndingPoint takes real x, real y, real r returns nothing
                local location l
                 
                // generates the random offset for x and y
                local real randomRadius = r * SquareRoot(GetRandomReal(0, 1))
                local real angle = GetRandomDirectionDeg() * bj_DEGTORAD 
                set .x2 = x + randomRadius * Cos(angle)
                set .y2 = y + randomRadius * Sin(angle)
              
                // accounts for elevation
                set l = Location(.x2, .y2)
                set .z2 = GetLocationZ(l)
                call RemoveLocation(l)
            endmethod

            method setStartingPoint takes real x, real y, real r, real h1, real h2 returns nothing

                // generates the random offset for x and y
                local real randomRadius = r * SquareRoot(GetRandomReal(0, 1))
                local real angle = GetRandomDirectionDeg() * bj_DEGTORAD 
                set .x1 = x + randomRadius * Cos(angle)
                set .y1 = y + randomRadius * Sin(angle)
 
                // adjusts height
                set .z1 = GetRandomReal(h1, h2)
            endmethod
        
            method lerp takes real a, real b returns real
                return a + (b - a) * .t
            endmethod
        endmodule
